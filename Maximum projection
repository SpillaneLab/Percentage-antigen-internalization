//This macro creates a maximum projection of a resliced Z-stack image to visualize internalization. 

   dir = getDirectory("Choose an input directory");
   outputDir = getDirectory("Select output directory for max projections");
   
   
   setBatchMode(true);
   count = 0;
   countFiles(dir);
   n = 0;
   processFiles(dir);

   //Open all of the files within subfolders 
   
   
   function countFiles(dir) {
      list = getFileList(dir);
      for (i=0; i<list.length; i++) {
          if (endsWith(list[i], "/"))
              countFiles(""+dir+list[i]);
          else
              count++;
      }
  }

   function processFiles(dir) {
      list = getFileList(dir);
      for (i=0; i<list.length; i++) {
          if (endsWith(list[i], "/"))
              processFiles(""+dir+list[i]);
          else {
             showProgress(n++, count);
             path = dir+list[i];
             processFile(path);
          }
      }
  }

  function processFile(path) {
       if (endsWith(path, ".tif")) {
       		
       		//Open the image
       		open(path);
       		imagesName=getTitle();
       		
       		//Get the filename without extension 
       		title = imagesName;
			dotIndex = indexOf(title, ".");
			basename = substring(title, 0, dotIndex); 
			
       		//Duplicate the original image and rename it
       		run("Duplicate...", "duplicate");
       		rename("Image"); 
       		
       		//Make maximum projection
       		selectWindow("Image"); 
       		run("Z Project...", "projection=[Max Intensity]");
       		
       		//Reset brightness and contrast
			resetMinAndMax();
			
			//Save stack 
			outName1 = "max_" + basename;
			saveAs("tif", outputDir + "/" + outName1);

//Close everything
run("Close All");
         }
  }
			
